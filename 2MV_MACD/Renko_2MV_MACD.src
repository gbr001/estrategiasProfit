// Autor: Gustavo Brand (TODO colocar email)
// Fonte: Canal Viver de Trade no youtube

// Alvo principal: WDO com renko (7R ou 8R) no intraday

// Indicacao de coloracao + Compra/Venda:
// Compra: no fechamento acima de medias exp 21 e 72 + 
//         MADC histograma 150,14,14 acima de 0 + 
//         apos candle branco do 2MV_Padrao. 
// Venda: no fechamento abaixo de medias exp 21 e 72 + 
//         MADC histograma 150,14,14 abaixo de 0 + 
//         apos candle branco do 2MV_Padrao. 
 
input
  TwoMVPeriod(20);
  FastPeriodMMs(21);
  MediumPeriodMMs(72);

  LongPeriodMACD(150);
  FastPeriodMACD(14);
  SignalPeriodMACD(14);

var
  sFastMMs   : Float;
  sMediumMMs : Float;
  s2MVHigh : Float;
  s2MVLow : Float; 
  sMediumMMsMax : Float;
  sMediumMMsMin : Float;
  sMediumMMConstant : Float;

  sLongMACD   : Float; 
  sFastMACD   : Float;
  sSignalMACD : Float;
  macdval : Float;
  macdval2 : Float;
  macdDiff : Float;
  macdTrend : Float;
  macdMax : Float;
  macdmargin : Float; //Margem de aceite da operacao quando 
                      //em torno de 0. Valor de +- 1 para WDO 15R.

  stopInit         : Float;
  stopVarianceInit : Float;
  stopTrailing     : Float;

  buySignal : Boolean;
  sellSignal : Boolean;
  neutralCandle : Boolean;
  macdPositiveTrend : Boolean;
  macdNegativeTrend : Boolean;

  i : Integer;
  horarioInicial : Integer;
  horarioFinal : Integer;

begin // BEGIN 01

  // Setup do valor de trailing stop desejado.
  // Nesse momento isso não tem muita funcionalidade
  // pois as saidas estao ocorrendo pela ocorrencia de
  // um candle invertido/contrario. É uma melhoria a se 
  // fazer realizar um trailing stop decente.
  stopInit := 13.5; // Stops em 3 candles: 15R=21; 8R=9.5; 7R=9 
  stopVarianceInit := 0;
  stopTrailing := 13.5; // Stops em 3 candles: 15R=21; 8R=9.5; 7R=9
  macdmargin := 0;
  // Valor do MACD histograma maximo aceito para entrada
  // TODO: analisar, pois nesse momento nao esta com um valor de corte
  //       que faca diferenca
  macdMax := 20;  

  // Variaveis de controle para sinalizar entrada na compra, venda
  // ou se o 2MV_Padrao está neutro, sem sinalizacao positiva ou negativa 
  buySignal := false;
  sellSignal := false;
  neutralCandle := false;

  // Nota importante sobre o algoritmo no Profit:
  // Para se ter os valores de indicadores e medias 
  // calculados corretametne eh preciso que eles 
  // sejam executados em todos os momentos e nao somente
  // dentro de alguma condicao if/else.

  sFastMMs := MediaExp(FastPeriodMMs,Close);
  sMediumMMs := MediaExp(MediumPeriodMMs,Close);

  // Faixa em que na mm72 pode-se detectar um comportamento lateral.
  // Nesse caso, é verificado se em um intervalo/canal muito fino
  // os candles não estão em tendencia lateral.
  // TODO 1: analisar se faz sentido, pois diminiu o rendimento em backtests
  //         com um valor de 0.000026 ou 0.00003. 
  // TODO 2: usar isso em conjunto com a verificacao do MACD histograma ter
  //         valores menores que 1 (ou algo nesse patamar) em um intervalor
  //         dos ultimos 3 candles. 
  sMediumMMConstant := 0.0000001; // Valor de 0.000026 para cima impediu operacoes de exemplo stopadas entre 11 e 12h de 13/abr/2021. 
  sMediumMMsMax := sMediumMMs*(1+sMediumMMConstant);        
  sMediumMMsMin := sMediumMMs*(1-sMediumMMConstant);

  // Calculo do MACD, sinal do MACD e calculo manual do histograma
  // em macdTrend. Através desse ultimo conseguimos detectar se 
  // o MACD está em tendencia (seja positiva ou negativa) para entrar.
  macdval := MACD(LongPeriodMACD, FastPeriodMACD, SignalPeriodMACD)|0|;
  sSignalMACD := MediaExp(SignalPeriodMACD, macdval);
  macdTrend := macdval - sSignalMACD ;
  plot(macdval);
  plot2(sSignalMACD); // media que parece o traco mais longo

  // Calculo do indicador 2MV_Padrao
  s2MVHigh := Media(TwoMVPeriod, High); // de forma simples: Media(20, High) 
  s2MVLow := Media(TwoMVPeriod, Low);   // de forma simples: Media(20, Low)
  neutralCandle := ((Close <= s2MVHigh) and (Close >= s2MVLow));

  // Horarios limite de negociacao da estrategia
  // Em backtests foi verificado em operações durante a 1a hora
  // geravam menos retorno.
  horarioInicial := 1030;
  horarioFinal := 1650;
  // Verificacao para fechar trades ao final do dia.  
  if (TIME <= horarioInicial) or 
     (TIME >= horarioFinal) then
    begin
      CancelPendingOrders;
      if (BuyPosition >= 1) then
        SellToCoverAtMarket;
      if (SellPosition >= 1) then
        BuyToCoverAtMarket;
    end

  else 
    begin  // BEGIN 02
      
      // Em caso de posicao comprada, subir o stop ou stopar o trade
      if (BuyPosition >= 1) then
        begin
          // Se o candle anterior eh de alta, subir o stop
          // Essa condição não é acionada hoje se mantivermos o 
          // valor de stopTrailing acima de 2 candles.
          if (Close[1] > Open[1]) then
            begin
              CancelPendingOrders;
              SellShortStop(Open-stopTrailing, Open-stopTrailing);
              //SellShortStop(Close-stopTrailing, Close-stopTrailing);
              PaintBar(clGreen);
            end;
          // Mas se o candle atual eh de baixa, stopar a mercado
          if (Open < Close[1]) then // ou (Close < Open)
            begin
              CancelPendingOrders;        
              SellToCoverAtMarket;
              PaintBar(clYellow);
            end;
        end;

      // Em caso de posicao vendida, baixar o stop ou stopar o trade
      if (SellPosition >= 1) then
        begin
          // Se o candle anterior eh de baixa, baixar o stop
          // Essa condição não é acionada hoje se mantivermos o 
          // valor de stopTrailing acima de 2 candles.
          if (Close[1] < Open[1]) then
            begin
              CancelPendingOrders;
              BuyToCoverStop(Open+stopTrailing, Open+stopTrailing);
              //BuyToCoverStop(Close+stopTrailing, Close+stopTrailing);
              PaintBar(clMaroon);
            end;
          // Mas se o candle atual eh de alta, stopar a mercado
          if (Open > Close[1]) then // ou Close > Open
            begin
              CancelPendingOrders;        
              BuyToCoverAtMarket;
              PaintBar(clYellow);
            end;
        end;

      // Caso não se esteja nem comprado, nem vendido vamos
      // analisar se temos sinal de compra ou venda.
      if (BuyPosition = 0) and (SellPosition = 0) then
        begin // BEGIN 03

            // Variavel que contabiliza a maior parte do setup para compra
            buySignal := (Close > sFastMMs) and 
               (Close > sMediumMMs) and
               (macdval-macdmargin > sSignalMACD) and
               (Close > s2MVHigh) and  //Caso positivo do 2MV_Padrao
               (macdTrend < macdMax ) and
               ((macdTrend[2] < macdTrend[1]) and (macdTrend[1] < macdTrend));
            
            // Variavel que contabiliza a maior parte do setup para venda   
            sellSignal := (Close < sFastMMs) and 
               (Close < sMediumMMs) and
               (macdval+macdmargin < sSignalMACD) and
               (Close < s2MVLow)  and  //Caso negativo do 2MV_Padrao    
               (Abs(macdTrend) < macdMax ) and
               ((macdTrend[2] > macdTrend[1]) and (macdTrend[1] > macdTrend));
            
            // Se verifica: 
            // 1. Se o setup de compra esta valido;
            // 2. Se temos um candle neutro do 2MV_Padrao dentre os ultimos 6
            // 3. Se nao estamos em uma lateralizacao
            // 4. Se o candle atual é de alta
            if (buySignal and
               (neutralCandle[1] or neutralCandle[2] or 
                neutralCandle[3] or neutralCandle[4] or
                neutralCandle[5] or neutralCandle[6]) and 
                (not((sMediumMMs[1] <= sMediumMMsMax) and (sMediumMMs[1] >= sMediumMMsMin)) and
                 not((sMediumMMs[2] <= sMediumMMsMax) and (sMediumMMs[2] >= sMediumMMsMin))) and
                (Close > Close[1])) then
               begin
                 if (BuyPosition = 0) and 
                    (Close > Open) then //and 
                    //not((macdDiff < 1) and (macdDiff[1] < 1) and (macdDiff[2] < 1))then
                   begin
                     BuyAtMarket;
                     SellShortStop(Close-stopInit+stopVarianceInit, Close-stopInit);                     
                     PaintBar(clLime);
                   end;                 
               end
            // Se verifica: 
            // 1. Se o setup de venda esta valido;
            // 2. Se temos um candle neutro do 2MV_Padrao dentre os ultimos 6
            // 3. Se nao estamos em uma lateralizacao
            // 4. Se o candle atual é de baixa 
            else if (sellSignal) and
                    (neutralCandle[1] or neutralCandle[2] or
                     neutralCandle[3] or neutralCandle[4] or
                     neutralCandle[5] or neutralCandle[6]) and
                     (not((sMediumMMs[1] <= sMediumMMsMax) and (sMediumMMs[1] >= sMediumMMsMin)) and
                      not((sMediumMMs[2] <= sMediumMMsMax) and (sMediumMMs[2] >= sMediumMMsMin))) and 
                    (Close < Close[1]) then
               begin                
                 if (SellPosition = 0) and
                    (Close < Open) then // and
                     //not((macdDiff < 1) and (macdDiff[1] < 1) and (macdDiff[2] < 1))then
                   begin
                     SellShortAtMarket;
                     BuyStop(Close+stopInit-stopVarianceInit, Close+stopInit);                  
                     PaintBar(clRed);
                   end;
               end
            // Se por acaso tenhamos chegado aqui é por que estamos em uma posicao
            // que não é de compra nem de venda. Preventivamente esta se fechando
            // as posicoes, mas talvez nao seja necessario uma vez que ja deve-se 
            // estar sem posicao ao chegar aqui.
            else
               begin
                 //PaintBar(clWhite);
                 CancelPendingOrders;
                 if (BuyPosition >= 1) then
                   SellToCoverAtMarket;
                 if (SellPosition >= 1) then
                   BuyToCoverAtMarket;
//                 if (not(Close[i] > s2MVHigh[i]) and
//                     not(Close[i] < s2MVLow[i])) then
//                   PaintBar(clGray);              
               end; 
        end; // END 03
   end; // END 02 
end; // END 01
